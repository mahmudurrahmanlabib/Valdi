//
//  SCValdiAttributedText.m
//  valdi-ios
//
//  Created by Simon Corsin on 12/19/22.
//

#import "valdi/ios/Text/SCValdiAttributedText.h"
#import "valdi/ios/Text/SCValdiImageAttachmentInfo.h"
#import "valdi/ios/Text/SCValdiInlineViewAttachmentInfo.h"
#import "valdi/ios/Text/SCValdiTextAnimationTransform.h"
#import "valdi_core/cpp/Attributes/TextAttributeValue.hpp"
#import "valdi_core/SCValdiObjCConversionUtils.h"
#import "valdi_core/UIColor+Valdi.h"
#import "valdi_core/SCValdiWrappedValue+Private.h"

@implementation SCValdiAttributedText {
    Valdi::Ref<Valdi::TextAttributeValue> _cppInstance;
}

- (instancetype)initWithCppInstance:(void *)cppInstance
{
    self = [super init];

    if (self) {
        _cppInstance = Valdi::unsafeBridge<Valdi::TextAttributeValue>(cppInstance);
    }

    return self;
}

- (instancetype)initWithWrappedValue:(SCValdiWrappedValue*)wrappedValue
{
    self = [super init];

    if (self) {
        _cppInstance = wrappedValue.value.getTypedRef<Valdi::TextAttributeValue>();
        NSAssert(_cppInstance != nullptr, @"Wrapped value is not a TextAttributeValue");
    }

    return self;
}

- (void)dealloc
{
    _cppInstance = nullptr;
}

- (NSUInteger)partsCount
{
    return (NSUInteger)_cppInstance->getPartsSize();
}

- (NSUInteger)animationTransformsCount
{
    return (NSUInteger)_cppInstance->getAnimationTransformsSize();
}

- (NSString *)contentAtIndex:(NSUInteger)index
{
    const auto &content = _cppInstance->getContentAtIndex(index);
    return ValdiIOS::NSStringFromString(content);
}

- (nullable NSString *)fontAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (!style.font) {
        return nil;
    }
    return ValdiIOS::NSStringFromString(style.font.value());
}

- (SCValdiTextDecoration)textDecorationAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    switch (style.textDecoration) {
        case Valdi::TextDecoration::Unset:
            return SCValdiTextDecorationUnset;
        case Valdi::TextDecoration::None:
            return SCValdiTextDecorationNone;
        case Valdi::TextDecoration::Strikethrough:
            return SCValdiTextDecorationStrikethrough;
        case Valdi::TextDecoration::Underline:
            return SCValdiTextDecorationUnderline;
        case Valdi::TextDecoration::DashedUnderline:
            return SCValdiTextDecorationDashedUnderline;
        case Valdi::TextDecoration::DottedUnderline:
            return SCValdiTextDecorationDottedUnderline;
    }
}

- (nullable UIColor *)colorAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (!style.color) {
        return nil;
    }

    return UIColorFromValdiAttributeValue(style.color.value().value);
}

- (nullable UIColor *)backgroundColorAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (style.background == nullptr || !style.background->color) {
        return nil;
    }

    return UIColorFromValdiAttributeValue(style.background->color.value().value);
}

- (nullable id<SCValdiFunction>)onTapAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (style.onTap == nullptr) {
        return nil;
    }

    return ValdiIOS::FunctionFromValueFunction(style.onTap);
}

- (nullable id<SCValdiFunction>)onLayoutAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (style.onLayout == nullptr) {
        return nil;
    }

    return ValdiIOS::FunctionFromValueFunction(style.onLayout);
}

- (nullable UIColor *)outlineColorAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (!style.outlineColor) {
        return nil;
    }

    return UIColorFromValdiAttributeValue(style.outlineColor.value().value);
}


- (nullable NSNumber *)outlineWidthAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (!style.outlineWidth) {
        return nil;
    }

    return @(style.outlineWidth.value());
}

- (nullable UIColor *)outerOutlineColorAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (!style.outerOutlineColor) {
        return nil;
    }

    return UIColorFromValdiAttributeValue(style.outerOutlineColor.value().value);
}

- (nullable NSNumber*)outerOutlineWidthAtIndex:(NSUInteger)index;
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (!style.outerOutlineWidth) {
        return nil;
    }

    return @(style.outerOutlineWidth.value());
}

- (nullable SCValdiImageAttachmentInfo *)imageAttachmentAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (!style.imageAttachment) {
        return nil;
    }

    const auto &attachment = style.imageAttachment.value();

    NSString *attachmentId = ValdiIOS::NSStringFromString(attachment.attachmentId);
    CGFloat width = attachment.width;
    CGFloat height = attachment.height;

    NSData *imageData = nil;
    if (!attachment.imageData.empty()) {
        imageData = ValdiIOS::NSDataFromBuffer(attachment.imageData);
    }

    return [[SCValdiImageAttachmentInfo alloc] initWithAttachmentId:attachmentId
                                                              width:width
                                                             height:height
                                                          imageData:imageData];
}

- (nullable SCValdiInlineViewAttachmentInfo *)inlineViewAttachmentAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (style.inlineViewAttachment == nullptr) {
        return nil;
    }

    auto verticalAlignment = SCValdiInlineViewVerticalAlignmentCenter;
    switch (style.inlineViewAttachment->getVerticalAlignment()) {
        case Valdi::InlineViewVerticalAlignment::Top:
            verticalAlignment = SCValdiInlineViewVerticalAlignmentTop;
            break;
        case Valdi::InlineViewVerticalAlignment::Bottom:
            verticalAlignment = SCValdiInlineViewVerticalAlignmentBottom;
            break;
        case Valdi::InlineViewVerticalAlignment::Baseline:
            verticalAlignment = SCValdiInlineViewVerticalAlignmentBaseline;
            break;
        case Valdi::InlineViewVerticalAlignment::Center:
            verticalAlignment = SCValdiInlineViewVerticalAlignmentCenter;
            break;
    }
    auto inlineViewAttachment = style.inlineViewAttachment;
    return [[SCValdiInlineViewAttachmentInfo alloc]
        initWithChildIndex:(NSInteger)inlineViewAttachment->getChildIndex()
         verticalAlignment:verticalAlignment
              sizeProvider:^CGSize{
                  auto size = inlineViewAttachment->getSize();
                  return CGSizeMake(size.width, size.height);
              }];
}

- (nullable SCValdiTextAnimationTransform *)animationTransformAtIndex:(NSUInteger)index
{
    const auto &style = _cppInstance->getStyleAtIndex(index);
    if (!style.animationTransform) {
        return nil;
    }

    const auto &animationTransform = style.animationTransform.value();
    NSString *key = animationTransform.key ? ValdiIOS::NSStringFromString(animationTransform.key.value()) : nil;
    NSString *partPattern = animationTransform.partPattern.isEmpty()
        ? nil
        : ValdiIOS::NSStringFromString(animationTransform.partPattern);
    return [[SCValdiTextAnimationTransform alloc] initWithKey:key
                                                    partIndex:index
                                                 translationY:animationTransform.translationY
                                                        scale:animationTransform.scale
                                                      opacity:animationTransform.opacity
                                                     duration:animationTransform.duration
                                       timeOffsetBetweenParts:animationTransform.timeOffsetBetweenParts
                                                   groupIndex:animationTransform.groupIndex
                                             partIndexInGroup:animationTransform.partIndexInGroup
                                                  partPattern:partPattern];
}

@end
